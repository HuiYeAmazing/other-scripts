grid

/* &do grd &list 
/* &if [exist %grd% -grid] &then kill %grd% all
/* &end

/* 1.辐射模块
&type 辐射模块


  /* 1.1 大气上界太阳辐射
    /* 公式：Ra=s0*24*60/(pi*d*(omega*sin(phi)*sin(delta)+cos(phi)*cos(delta)*sin(omega)))
    
    
    &type Ra
    /*1.1.1 请输入天数（day）、纬度数据（data_lat）、温度数据（data_tair）、相对湿度数据（data_humid）
      &sv day = 145
      data_lat = lat1_clip33
      data_tair = t0008_clip
      data_humid = h0008_clip
      
      &sv pi = 3.1416
      &sv s0 = 0.0820
      &sv d = 1 / (1 + 0.033 * cos(2 * %pi% * %day% / 365))
      &sv delta = 0.409 * sin(2 * %pi% * %day% / 365 - 1.39)
           
      hj_phi = data_lat * %pi% / 180
      hj_omega = acos(- (tan(hj_phi) * tan(%delta%)))
      
    /*1.1.2 计算Ra 
      hj_ra = %s0% * 24 * 60 / (%pi% * %d% * (hj_omega * sin(hj_phi) * %delta% + cos(hj_phi) * cos(%delta%) * sin(hj_omega)))


  /* 1.2太阳短波辐射
    /* 输入参数：日照时数数据（data_sun） 
    &type Rs
    /*1.2.1 请输入日照时数数据（data_sun）
      data_sun = s0008_clip
    
      hj_n = 24 * hj_omega / %pi%
    
    /*1.2.2 计算Rs 
      hj_rs = (0.353 + 0.543 *  data_sun / hj_n) * hj_ra
    
    /*1.2.3 计算PAR
      hj_par = hj_rs * 0.5

    
  /* 1.3陆地表面净辐射
    /*输入参数：环境卫星反照率数据（data_alpha）
    &type Rn
    /*1.3.1 请输入环境卫星1、3、4波段数据
      data_band1 = hj090526c1dn
      data_band2 = hj090526c2dn
      data_band3 = hj090526c3dn
      data_band4 = hj090526c4dn
      
      data_band1l = hj090526c1dn / 0.5763 + 9.3183
      data_band2l = hj090526c2dn / 0.5410 + 9.1758
      data_band3l = hj090526c3dn / 0.6824 + 7.5072
      data_band4l = hj090526c4dn / 0.7209 + 4.1484
      data_band1r = data_band1l * %pi% * sqr(1.013) / (1914.324 * cos(24.982))
      data_band2r = data_band2l * %pi% * sqr(1.013) / (1825.419 * cos(24.982))
      data_band3r = data_band3l * %pi% * sqr(1.013) / (1542.664 * cos(24.982))
      data_band4r = data_band4l * %pi% * sqr(1.013) / (1073.826 * cos(24.982))
      
      data_alpha = mean(data_band1r,data_band2r,data_band3r)
      hj_rns = hj_rs * (1 - data_alpha)
      hj_e0 = 0.618 * exp(17.27 * data_tair / (data_tair + 237.3))
      hj_ea = hj_e0 * data_humid / 100.0
    
    /*1.3.1 计算Rnl   
      hj_rnl = 0.000000004903 * pow(data_tair,4) * (0.56 - 0.079 * sqrt(hj_ea)) * (0.10 + 0.90 * data_sun / hj_n)
      
    /*1.3.2 计算Rn
      hj_rn = hj_rns - hj_rnl
    


/* 2.校正系数模块
&type 模块  

  
  /*2.1 光和有效辐射吸收比率（FPAR）
    /*输入参数：环境卫星1、3、4波段数据（data_band1、data_band3、data_band4）
    &type FPAR    
    /*2.1.1 计算FPAR  
      hj_evi = 2.5 * (data_band4r - data_band3r) / (data_band4r + 6.0 * data_band3r - 7.5 * data_band1r + 1.0)
      hj_fpar = hj_evi * (hj_evi >= -1 & hj_evi <= 1)
       
                    
  /*2.2 光能利用率（LUE）
    &type LUE
    /*2.2.1 胁迫系数
    
      tair_temp = data_tair + 273.15
      humid_temp = data_humid / 100.0
      tair_xp = ((tair_temp - 273.15) * (tair_temp - 313.15)) / ((tair_temp - 273.15) * (tair_temp - 313.15) - sqr(tair_temp - 298.15))
      humid_xps1 = (1.00 - humid_temp) * (0.6108 * exp(17.27 * data_tair / (data_tair + 237.3)))
      humid_xps2 = 1.00 / (1.00 + 0.329 * humid_xps1)
      humid_xp = humid_xps2 * (humid_xps2 >= 0)
      
    /*2.2.2 计算LUE
      hj_lue = 1.0120 * tair_xp * humid_xp
      
  
        
/* 3.数据产品模块
&type 数据产品模块  
  
  /*3.1 GPP产品
    &type GPP
    hj_gppt = hj_par * hj_fpar * hj_lue
    hj_gpp = hj_gppt * (hj_gppt >= 0)

    
  /*3.2 NPP产品
    &type NPP
    hj_nppt = hj_par * hj_fpar * 0.389 * tair_xp * humid_xp
    hj_npp = hj_nppt * (hj_nppt >= 0)
    
  
  /*3.3 陆地表面温度（LST）
    /*输入参数：环境卫星热红外波段数据（data_thermal）    
    &type LST
    /*3.3.1 请输入环境卫星热红外波段数据（data_thermal）
      data_thermal = hj090410_dn
      data_ther_l = (data_thermal + 44.598) / 61.472
      
    /*3.3.2 计算LST
      data_ther_k = 1282.7108 / (ln(666.09 / data_ther_l + 1))
      hj_lst = data_ther_k - 273.15
      
  
  /*3.4 陆地表面蒸散（ET）
    &type ET
      hj_le = hj_rn * (0.137 + 0.759 * hj_evi + 0.004 * data_tair)
      hj_ett = hj_le / 0.408
      
      hj_et = hj_ett * (hj_ett >= 0)


q  